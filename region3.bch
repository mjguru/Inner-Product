Constants
  b1 = 0.34;
  c1 = 0.16;
  b2 = 0.465;
  c2 = 0.035;

  THR  = 1e-15;
  LN2  = ln(2);

Variables
  p  in [0.25, 0.64];
  r  in [0.05, 1.0];

  /* v1, v2 are the positive roots of the quadratic (modeled as variables) */
  v1 in [0, 100000];
  v2 in [0, 100000];

  /* t = min(rate1, rate2) that we maximize */
  t  in [0, 1];

function log2(x)
  return ln(x)/LN2;
end


function logx_upper(x)
  return log2(max(x, THR));
end

function xlogx_lower(x)
  return max(x, THR) * log2(max(x, THR));
end

function xlogx_upper(x)
  return (x * log2(x));
end


function rate(p,r,v,b,c)
  q = 1 - p - r;

  line1 = -xlogx_lower(p) - xlogx_lower(q) - xlogx_lower(r) + q;
  line2 = q * logx_upper(v) - 2*c * log2(2*v + 1) - (2*b - p) * log2(v + 1);
  line3 = 0.5 * xlogx_upper(2*b - p) + 0.5 * xlogx_upper(p) - b * log2(2*b);

  return line1 + line2 + line3;

end

Minimize
  -t;

Constraints
  /* original constraints */
  1.25 - (25/32)*p - 6.25*r <= 0;
  1 - p - r >= 0;

  /* quadratic equalities defining v1 and v2 */
  2 * r * v1^2 + (2*p + 3*r - 1.68) * v1 - (1 - p - r) = 0;
  2 * r * v2^2 + (2*p + 3*r - 1.93) * v2 - (1 - p - r) = 0;

  /* maximize min(rate1, rate2) by pushing t up under both */
  t <= rate(p,r,v1,b1,c1);
  t <= rate(p,r,v2,b2,c2);
end
